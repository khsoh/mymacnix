#!/bin/zsh


set -u
set -o pipefail

## Choice of repository host "github" or "gitlab"

REPOHOST=gitlab

readonly ESC="$(tput sgr0)"
readonly BOLD="$(tput bold)"
readonly GREEN="$(tput setaf 2)"
readonly RED="$(tput setaf 1)"

SCRIPTNAME=$(readlink -f "$0")
SCRIPTDIR=$(dirname "$SCRIPTNAME")

# Script to clone the mymacnix repo, stow the HOME subfolder in the repo and
# install and configure nix-darwin

function print_arrow () {
    printf "${BOLD}${GREEN}"
    eval $(echo printf '"=%.0s"' {1..$SHLVL})
    printf "> $*${ESC}\n"
}

function usage () {
    print_arrow "Usage: "
    print_arrow "  zsh -l nixsys-setup [--install=nixonly] [--branch=<feature-branch>] [--repohost=(gitlab|github)]"
    print_arrow "    The default feature-branch is 'main' if it is not specified"
    print_arrow "    The default repohost is 'gitlab' if it is not specified"
    exit 1
}

function get_gitfolder () {
    [[ ! -n "${gitfolder+1}" ]] || return
    local prompt="${BOLD}${GREEN}Enter name of subfolder within $HOME to clone mymacnix: ${ESC}"
    printf "$prompt"
    while read -r folder </dev/tty; do
        if [ "$folder" != "" ]; then
            if [[ ! -e "$HOME/$folder" ]]; then
                break
            fi
            printf "${RED}$HOME/$folder already exists - please choose another directory${ESC}\n"
        fi
        printf "$prompt"
    done
    eval "export gitfolder=$HOME/$folder"
}

## Test to confirm nix has been installed
if [[ ! $(/sbin/mount | grep -w "/nix") ]]; then
    printf "${BOLD}${RED}Error: Nix has not yet been installed - nixsys-setup script cannot be executed${ESC}\n"
    exit 1
fi

OPTINSTALL="all"
BRANCH="main"
CMDARGS=( "$@" )
while test $# -gt 0; do
    case "$1" in
        --install=nixonly)
            OPTINSTALL="${1##--install=}";;
        --branch=*)
            BRANCH="${1##--branch=}";;
        --repohost=*)
            REPOHOST="${1##--repohost=}";;
        *)
            echo "Invalid option $1"
            usage;;
    esac
    shift
done

## Executed the startup scripts to check what whether nix or nix-darwin installed
DARWININSTALLED=1
ZSCRIPTSRUN=0

if [[ ! $(nix --version 2>/dev/null) ]]; then
    ## Executed the startup scripts to ensure nix path is present
    [[ -f /etc/zshenv ]] && . /etc/zshenv
    [[ -f /etc/zprofile ]] && . /etc/zprofile
    [[ -f /etc/zshrc ]] && . /etc/zshrc
    ZSCRIPTSRUN=1
    if [[ ! $(nix --version 2>/dev/null) ]]; then
        printf "${BOLD}${RED}nix cannot be executed even after executing /etc/z* startup scripts - something is wrong with this setup${ESC}\n"
        exit 1
    fi
fi

if [[ ! $(darwin-version --version 2>/dev/null) ]]; then
    if [[ ZSCRIPTSRUN -eq 1 ]]; then
        DARWININSTALLED=0
    else
        [[ -f /etc/zshenv ]] && . /etc/zshenv
        [[ -f /etc/zprofile ]] && . /etc/zprofile
        [[ -f /etc/zshrc ]] && . /etc/zshrc
        if [[ ! $(darwin-version --version 2>/dev/null) ]]; then
            DARWININSTALLED=0
        fi
    fi
fi

# Define where the config is
NIX_DARWIN_DIR="/etc/nix-darwin"
SCRIPT_PATH="$NIX_DARWIN_DIR/generate_machine_info.sh"
MINFO_FILE="$NIX_DARWIN_DIR/machine-info.nix"

if [ ! -f $SCRIPT_PATH ]; then
    # 1. Ensure the directory exists
    sudo mkdir -p "$NIX_DARWIN_DIR"

    # 2. Write the script to the disk
    sudo -H tee "$SCRIPT_PATH" >/dev/null <<'EOF'
#!/usr/bin/env bash
IS_VM=$(/usr/sbin/sysctl -n kern.hv_vmm_present)
XHOSTNAME=$(/usr/sbin/scutil --get LocalHostName)
BGROUPID=$(dscl . -read /Groups/nixbld PrimaryGroupID 2>/dev/null | awk '{ print $2 }')
if [ -z "$BGROUPID" ]; then
  BGROUPID=350
fi
MINFO_FILE="$1"
cat <<EOF2 > "$MINFO_FILE"
{
  is_vm = $IS_VM;
  hostname = "${XHOSTNAME}";
  buildGroupID = $BGROUPID;
}
EOF2
EOF

    # 3. Make the script executable
    sudo -H chmod +x "$SCRIPT_PATH"

    # 4. Run the script *now* to create the initial machine-info.nix file
    sudo -H "$SCRIPT_PATH" "$MINFO_FILE"
fi

if [[ $DARWININSTALLED -eq 1 ]]; then
    printf "${BOLD}${RED}nix-darwin already installed - re-running darwin-rebuild switch${ESC}\n"
    sudo -H darwin-rebuild switch
    exit
fi

# Added code to install Xcode command line tools
print_arrow "Checking whether Xcode command line tools have been installed"
sudo -H xcode-select -p &> /dev/null
if [ $? -ne 0 ]; then
  print_arrow "Command Line Tools for Xcode not found. Installing from softwareupdateâ€¦"
# This temporary file prompts the 'softwareupdate' utility to list the Command Line Tools
  touch /tmp/.com.apple.dt.CommandLineTools.installondemand.in-progress;
  PROD=$(softwareupdate -l | grep "\*.*Command Line" | tail -n 1 | sed 's/^[^C]* //')
  softwareupdate -i "$PROD" --verbose;
else
  print_arrow "Command Line Tools for Xcode have been installed."
fi


git -C "$SCRIPTDIR" rev-parse 2>/dev/null
if [ $? -ne 0 ]; then
    ## Script folder is not a git folder
    get_gitfolder

    mkdir -p ~/.config/git

    print_arrow "Cloning branch ${BRANCH} of mymacnix repo"
    export BRANCH
    export REPOHOST
    export GREEN
    export ESC
    [ -f /etc/zshenv ] && . /etc/zshenv
    [ -f /etc/zprofile ] && . /etc/zprofile
    [ -f /etc/zshrc ] && . /etc/zshrc
    NIXPKGS_ALLOW_UNFREE=1 nix-shell -p git stow openssh _1password-cli --command '
    echo "Populating ~/.ssh/known_hosts with public keys of github.com and gitlab.com"
    # Save current umask to set temporary umask
    save_umask=$(umask)
    umask 077

    # Populate known_hosts with public keys of github.com and gitlab.com
    mkdir -p ~/.ssh
    ssh-keyscan github.com >> ~/.ssh/known_hosts
    ssh-keyscan gitlab.com >> ~/.ssh/known_hosts

    # Restore original umask
    umask "$save_umask"

    echo "Cloning branch ${BRANCH} from ${REPOHOST}"
    git clone --branch ${BRANCH} --recurse-submodules https://${REPOHOST}.com/khsoh/mymacnix.git "$gitfolder"
    mkdir -p ~/Library/LaunchAgents
    cd $gitfolder
    ./git-remote-init.sh
    source "$gitfolder/dostow.sh"
    cd ..

    IS_VM=$(/usr/sbin/sysctl -n kern.hv_vmm_present)

    ## The following is required to share clipboard between VM and host OS
    if [ $IS_VM -eq 1 ]; then
        # 1. Prompt to mount Guest Tools ISO
        printf "${GREEN}Install guest tools to support shared clipboard with host OS by selecting \"Virtual Machine | Drives | Install Guest Tools...\" from the UTM menu bar${ESC}\n"

        MNTPATH="/Volumes/GUEST TOOLS"
        /bin/wait4path "$MNTPATH"

        PKG_PATH=$(/usr/bin/find "$MNTPATH" -maxdepth 1 -name "*.pkg" -print -quit)
        PKG_NAME=$(basename "$PKG_PATH")

        echo "\"$MNTPATH\" mounted - installing package $PKG_NAME"

        # 2. Install the Guest Tools to share clipboard betwen VM and host OS
        sudo -H installer -pkg "$PKG_PATH" -target /
    fi

    [[ ! -f ~/.ssh/nixid_ed25519 || ( $IS_VM -eq 0 && ! -f ~/.ssh/id_ed25519 ) ]] && OPSIGNIN=true || OPSIGNIN=false

    if $OPSIGNIN; then
        if [ $IS_VM -eq 0 ]; then
            # Non-VM - login to 1Password account
            eval $(op account add --signin)
        else
            # Get account service token
            read -sp "Enter Service Account Token: " OP_SERVICE_ACCOUNT_TOKEN && export OP_SERVICE_ACCOUNT_TOKEN
        fi
    fi

    killall ssh-agent
    if [ ! -f ~/.ssh/nixid_ed25519 ]; then
        # Use the query parameter to force OpenSSH format
        op read "op://Nix Bootstrap/NIXID SSH Key/private key?ssh-format=openssh" --out-file ~/.ssh/nixid_ed25519
    fi
    if [ ! -f ~/.ssh/nixid_ed25519.pub ]; then
        ssh-keygen -y -f ~/.ssh/nixid_ed25519 > ~/.ssh/nixid_ed25519.pub
    fi
    eval $(ssh-agent)
    ssh-add ~/.ssh/nixid_ed25519

    if [ $IS_VM -eq 0 ]; then
        # In non-VM machine - get the id_ed25519
        if [ ! -f ~/.ssh/id_ed25519 ]; then
            # Use the query parameter to force OpenSSH format
            op read "op://Private/OPENSSH ED25519 Key/private key?ssh-format=openssh" --out-file ~/.ssh/id_ed25519
        fi
        if [ ! -f ~/.ssh/id_ed25519.pub ]; then
            ssh-keygen -y -f ~/.ssh/id_ed25519 > ~/.ssh/id_ed25519.pub
        fi
        eval $(ssh-agent)
        ssh-add ~/.ssh/id_ed25519
    fi
    git clone git@${REPOHOST}.com:khsoh/caskmasapps.git

    cd caskmasapps
    ./dostow.sh
    '

    print_arrow "Completed cloning the repository"
else
    gitfolder=$SCRIPTDIR
fi

NEWCMDARGS=${CMDARGS[@]//--install=*/}
if [[ $OPTINSTALL == "nixonly" ]]; then
    print_arrow "Completed installing only nix and cloning repo"
    print_arrow "To proceed, open a new terminal and execute:"
    print_arrow "   cd \"$gitfolder\""
    print_arrow "   zsh -l nixsys-setup $NEWCMDARGS"
    exit
fi

cd "$gitfolder"

## Generate the primary user nix file
./genusernix.sh

## Update the nix channels
sudo -H nix-channel --update

## Move /etc/zshrc and /etc/bashrc files
if [ -f /etc/zshrc ]; then
    sudo -H mv /etc/zshrc /etc/zshrc.before-nix-darwin
fi
if [ -f /etc/bashrc ]; then
    sudo -H mv /etc/bashrc /etc/bashrc.before-nix-darwin
fi
print_arrow "Installing nix-darwin"
zsh -l scripts/darwin-autoinstall


