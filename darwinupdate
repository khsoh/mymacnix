#!/bin/zsh



set -u
set -o pipefail

readonly ESC="$(tput sgr0)"
readonly BOLD="$(tput bold)"
readonly GREEN="$(tput setaf 2)"
readonly RED="$(tput setaf 1)"

if [ ! -e /etc/nix-channels/system-channels ]; then
    sudo mkdir -p /etc/nix-channels
    sudo chmod a+rx /etc/nix-channels
    sudo cp /var/root/.nix-channels /etc/nix-channels/system-channels
    sudo chmod a+r /etc/nix-channels/system-channels
fi

SCRIPTNAME=$(readlink -f "$0")
SCRIPTDIR=$(dirname "$SCRIPTNAME")

printf "${BOLD}${GREEN}Updating nix-darwin...${ESC}\n"
printf "${BOLD}${RED}Starting to update channels:\n$(awk '{ print $2, $1 }' /etc/nix-channels/system-channels)${ESC}\n"
sudo -H nix-channel --update --verbose
printf "${BOLD}${GREEN}Completed updating channels:\n$(awk '{ print $2, $1 }' /etc/nix-channels/system-channels)${ESC}\n\n\n"
sleep 3

printf "${BOLD}${RED}Starting to update channels:\n$(nix-channel --list)${ESC}\n"
nix-channel --update --verbose
printf "${BOLD}${GREEN}Completed updating channels:\n$(nix-channel --list)${ESC}\n\n\n"
sleep 3

WORKFILE=~/.working-nixpkgs
NONWORKFILE=~/.nonworking-nixpkgs
LAST_GOOD_REV=$(awk 'END {print $1}' $WORKFILE || echo 0)
LATEST_NIXPKGS_REV=$(NIX_PATH=nixpkgs=channel:nixpkgs-unstable nix-instantiate --eval --expr "(import <nixpkgs> {}).lib.version"|sed -e 's/"//g' -e 's/.*\.//')
BUILDARGS=""
BUILDREV="$LATEST_NIXPKGS_REV"
if grep -q "^$LATEST_NIXPKGS_REV" $NONWORKFILE; then
    if [ "$LAST_GOOD_REV" = "0" ]; then
        OPTIONS="[l/x]"
    else
        OPTIONS="[l/g/x]"
    fi
    echo "nixpkgs rev $LATEST_NIXPKGS_REV failed to build previously"
    echo "What would you like to build? $OPTIONS"
    echo " l/L - Latest nixpkgs rev"
    if [ "$LAST_GOOD_REV" != "0" ]; then
        echo " g/G - Last good rev"
    fi
    echo " x/X - Exit"
    echo -n "$OPTIONS "
    read -k 1 i
    case "$i" in
        l|L)
            ;;
        g|G)
            if [ "$LAST_GOOD_REV" = "0" ]; then
                exit 0
            fi
            BUILDARGS="-I nixpkgs=https://github.com/NixOS/nixpkgs/archive/${LAST_GOOD_REV}.tar.gz"
            BUILDREV="$LAST_GOOD_REV"
            ;;
        *)
            exit 0
            ;;
    esac
fi

printf "${BOLD}${RED}Starting to rebuild system${ESC}\n"

## Generate the primary user nix file
$SCRIPTDIR/genusernix.sh

sudo -H darwin-rebuild switch --verbose $(echo $BUILDARGS)
BUILDRESULT=$?

if [ "$BUILDRESULT" = "0" ]; then
    if grep -q "^$BUILDREV" $WORKFILE ; then
        sed -i "/^$BUILDREV.*$/c$BUILDREV : $(date +"%d %b %Y %I:%M:%S %p")" $WORKFILE
    else
        echo "$BUILDREV : $(date +"%d %b %Y %I:%M:%S %p")" >> $WORKFILE
    fi
    if [ "$(wc -l < $WORKFILE | xargs)" -gt 40 ]; then
        tail -n 20 $WORKFILE | sponge $WORKFILE
    fi
    sed -i "/^$BUILDREV/d" $NONWORKFILE
else
    if grep -q "^$BUILDREV" $NONWORKFILE ; then
        sed -i "/^$BUILDREV.*$/c$BUILDREV : $(date +"%d %b %Y %I:%M:%S %p")" $NONWORKFILE
    else
        echo "$BUILDREV : $(date +"%d %b %Y %I:%M:%S %p")" >> $NONWORKFILE
    fi
    if [ "$(wc -l < $NONWORKFILE | xargs)" -gt 40 ]; then
        tail -n 20 $NONWORKFILE | sponge $NONWORKFILE
    fi
fi
brew upgrade

typeset -A kitty_perms

while IFS='|' read -r service client auth_value; do
    kitty_perms[$service]=$auth_value
done < <(sudo sqlite3 --readonly /Library/Application\ Support/com.apple.TCC/TCC.db \
    "SELECT service, client, auth_value FROM access WHERE client LIKE '%net.kovidgoyal.kitty%';")

for svc val in "${(@kv)kitty_perms}"; do
    if [ $val -ne 2 ]; then
        echo "$svc permission for kitty is disabled"
    fi
done

printf "${BOLD}${GREEN}Completed rebuilding system${ESC}\n"

