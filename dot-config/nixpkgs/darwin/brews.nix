{
  config,
  pkgs,
  lib,
  ...
}:
let
  HOMEDIR = "/Users/${config.system.primaryUser}";
  CaskMasDir = "${HOMEDIR}/.config/CaskMasApps/";
  formulaenix = CaskMasDir + "formulae.nix";
  commonformulaenix = CaskMasDir + "common-formulae.nix";
  casksnix = CaskMasDir + "casks.nix";
  commoncasksnix = CaskMasDir + "common-casks.nix";
  masappsnix = CaskMasDir + "masapps.nix";
  commonmasappsnix = CaskMasDir + "common-masapps.nix";

  defaultcasksnix = ./default-casks.nix;
  defaultmasappsnix = ./default-masapps.nix;

  CASKROOM = /opt/homebrew/Caskroom;
  ## List of user casks that follows the nix-darwin format for
  ##  each element of homebrew.casks attribute set entry
  ## The "appname" field is removed by the BrewCask function
  ##  to make the USERCASKS compatible with the homebrew.casks
  ##  entry
  ##
  ## The appname entry is used to filter out applications that
  ##  were already installed before Homebrew.  This specifies
  ##  the application name under the /Applications folder.
  AppExists = (appName: builtins.pathExists (/Applications + "/${appName}"));
  CaskInstalled = (n: builtins.pathExists (CASKROOM + "/${n}"));

  isVM = config.machineInfo.is_vm;

  ## Formulae to install
  FORMULAE =
    if builtins.pathExists formulaenix then import formulaenix ++ import commonformulaenix else [ ];

  ## Casks are machine dependent
  USERCASKS =
    if (!isVM) && builtins.pathExists casksnix then
      import casksnix ++ import commoncasksnix
    else
      import defaultcasksnix;

  MASAPPS =
    if (!isVM) && builtins.pathExists masappsnix then
      lib.trivial.mergeAttrs (import masappsnix) (import commonmasappsnix)
    else
      import defaultmasappsnix;

  caskOptions = [
    "name"
    "args"
    "greedy"
  ];
  BrewCask = (
    casks:
    map (e: lib.attrsets.filterAttrs (n: v: builtins.elem n caskOptions) e) (
      builtins.filter (e: !(e ? appname) || (!AppExists e.appname) || (CaskInstalled e.name)) casks
    )
  );

  # Get the mas-cli package version
  masdir = config.homebrew.prefix + "/Cellar/mas";

  # Check if a given path is a directory
  isDir = path: builtins.pathExists (toString path + "/.");

  # Check if the base directory exists
  baseDirExists = builtins.pathExists masdir && isDir masdir;

  # Function to find the newest version string
  findLatestVersion =
    dirPath:
    let
      # Read directory contents and keep only directories (which are version names)
      versions = builtins.filter (
        name: (isDir (dirPath + "/${name}")) && (builtins.pathExists (dirPath + "/${name}/bin/mas"))
      ) (builtins.attrNames (builtins.readDir dirPath));

      # Sort versions to find the latest one.
      # The compareVersions function returns 1 if the first arg is newer, -1 if older, 0 if same.
      # We use lib.sort with a custom comparator to sort in descending order (latest first).
      sortedVersions = lib.sort (a: b: if lib.strings.compareVersions a b == 1 then -1 else 1) versions;

      # The latest version is the head of the sorted list
      latestVersion =
        if builtins.length sortedVersions > 0 then builtins.elemAt sortedVersions 0 else null;
    in
    latestVersion;

  # Get the latest version string if the base directory exists
  latestMasVersion = if baseDirExists then findLatestVersion masdir else null;

  masLatestCannotUpdateVersion = "3.1.0";

  # Compare the latest version with masLatestCannotUpdateVersion
  isNewerThanTarget =
    if latestMasVersion != null then
      lib.strings.compareVersions latestMasVersion masLatestCannotUpdateVersion == 1
    else
      false;

  ## Allow update with mas-cli under any of the following conditions
  # 1. mas is not installed OR
  # 2. if installed mas is newer than target version
  canUpdateWithMas = (latestMasVersion == null) || isNewerThanTarget;

in
{

  mas.canUpdate = canUpdateWithMas;

  warnings = lib.mkIf (!builtins.pathExists casksnix && (!isVM)) [
    ''
      Using default-casks.nix and default-masapps.nix for 
      Homebrew casks and masapps.

      To build Homebrew casks and masapps specifically for
      ths machine, create and configure casks.nix and masapps.nix 
      in ${CaskMasDir}.
    ''
  ];

  ### Homebrew setup.  Default it to false
  homebrew.enable = true;

  # Use Brewfile in /nix/store that is generated by configuration.nix when invoking brew bundle command
  homebrew.global.brewfile = true;

  ### Setup for updates via Nix
  homebrew.global.autoUpdate = false;
  homebrew.onActivation.upgrade = true;
  homebrew.onActivation.autoUpdate = true;
  homebrew.onActivation.extraFlags = [
    "--verbose"
  ];
  ### End Setup for updates via Nix

  homebrew.brews = FORMULAE;

  homebrew.casks = BrewCask USERCASKS;

  homebrew.masApps = lib.optionalAttrs config.mas.canUpdate MASAPPS;

  ## The following allows Nix to uninstall stuff absent from cask list
  homebrew.onActivation.cleanup = "zap";

  ## Check for mas oudated if mas-cli currently cannot update from App Store
  system.activationScripts.homebrew.text = lib.mkIf (!config.mas.canUpdate) (
    lib.mkAfter ''
      sudo -u ${config.homebrew.user} TERM=xterm-256color -i ${pkgs.bash}/bin/bash <<'EOF'
        BOLD="$(tput bold)"
        RED="$(tput setaf 1)"
        ESC="$(tput sgr0)"
        printf "$BOLD$RED"
        echo ">>>Checking for App Store updates with mas outdated...<<<" >&2
        mas outdated || true
        printf "$ESC"
      EOF
    ''
  );
}
# vim: set ts=2 sw=2 et ft=nix:
